command /admin [<text>] [<text>] [<text>] [<text>] [<text>]:
  trigger:
    set {_success} to true
    set {_p} to event-player

    if arg-1 is "hmr":
      if arg-2 is "reset":
        manager_hotReload()

    else if arg-1 is "init":
      config_load(world of player)

    else if arg-1 is "player":
      if arg-2 is "stat":
        if arg-3 is "set":
          if arg-4 is set:
            player_store_stat_set({_p}, arg-4, arg-5 parsed as number)
        else if arg-3 is "get":
          set {_stat} to player_store_stat_get({_p}, arg-4)
      else if arg-2 is "init":
        player_init()
      else if arg-2 is "clean":
        player_clean(player)
    
    else if arg-1 is "weapon":
      set {_category} to arg-2
      set {_id} to arg-3
      item_give(player, "WEAPON", {_category}, {_id})

    else if arg-1 is "skill":
      set {_category} to arg-2
      set {_id} to arg-3
      item_give(player, "SKILL", {_category}, {_id})
    
    else if arg-1 is "gift":
      set {_category} to arg-2
      set {_id} to arg-3
      item_give(player, "GIFT", {_category}, {_id})

    else if arg-1 is "gui":
      set {_category} to arg-2
      set {_id} to arg-3
      gui_summon({_category}, {_id}, location of {_p})

    else if arg-1 is "settings":
      if arg-2 is "INGAME":
        if arg-3 is "true":
          player_store_setting_set({_p}, "INGAME", true)
        else:
          player_store_setting_set({_p}, "INGAME", false)

    else if arg-1 is "game":
      if arg-2 is "start":
        game_start()
      else:
        game_end()

    else if arg-1 is "test":
      if arg-2 is "all":
        test_main(player)
      else if arg-2 is "items":
        if arg-3 is set:
          test_item_main_detail(player, arg-3)
        else:
          test_item_main(player)
      else if arg-2 is "i18n":
        test_i18n_main(player)
      else if arg-2 is "player":
        if arg-3 is "store":
          test_player_store(player)
        else if arg-3 is "stat":
          test_player_stat(player)
        else if arg-3 is "action":
          test_player_action(player)
        else:
          test_player_main(player)
      else if arg-2 is "mob":
        test_mob_main(player)

on tab complete of "/admin":
  set tab completions for position 1 to "init", "hmr", "stat", "player", "weapon", "skill", "settings", "game", "gift", "gui" and "test"

  if tab arg-1 is "hmr":
    set tab completions for position 2 to "reset"

  else if tab arg-1 is "player":
    set tab completions for position 2 to "stat", "clean" and "init"
      
  else if tab arg-1 is "stat":
    set tab completions for position 2 to "get" and "set"
    if tab arg-2 is set:
      set tab completions for position 3 to {STAT::PLAYER::*}
  
  else if tab arg-1 is "weapon":
    set tab completions for position 2 to {WEAPON::CATEGORY::*}
    if tab arg-2 is set:
      set {_weapons::*} to {WEAPON::%tab arg-2%::*}
      remove "TEST" from {_weapons::*}
      set tab completions for position 3 to {_weapons::*}
  
  else if tab arg-1 is "skill":
    set tab completions for position 2 to {SKILL::CATEGORY::*}
    if tab arg-2 is set:
      set {_skills::*} to {SKILL::%tab arg-2%::*}
      remove "TEST" from {_skills::*}
      set tab completions for position 3 to {_skills::*}

  else if tab arg-1 is "gift":
    set tab completions for position 2 to "MEMORY", "WILL" and "INTELLECT"
    if tab arg-2 is set:
      set {_id::*} to {GIFT::%tab arg-2%::ID::*}
      set tab completions for position 3 to {_id::*}

  else if tab arg-1 is "gui":
    set tab completions for position 2 to {GUI::CATEGORY::*}
    if tab arg-2 is set:
      set tab completions for position 3 to {GUI::%tab arg-2%::ID::*}
  
  else if tab arg-1 is "settings":
    set tab completions for position 2 to "INGAME"
    if tab arg-2 is "INGAME":
      set tab completions for position 3 to "true" and "false"
  
  else if tab arg-1 is "game":
    set tab completions for position 2 to "start" and "stop"
  
  else if tab arg-1 is "test":
    set tab completions for position 2 to "items", "i18n", "player", "mob" and "all"
    if tab arg-2 is "items":
      set tab completions for position 3 to "weapon", "skill" and "item"
    else if tab arg-2 is "player":
      set tab completions for position 3 to "store", "action" and "stat"