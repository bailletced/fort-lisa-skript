#Give a skill to the player
function gift_give(p: player, type: text, category: text, id: text) :: item:
  set {_tier} to {%{_type}%::%{_category}%::%{_id}%::TIER}
  set {_gift} to item_create({_p}, {_type}, {_category}, {_id})
  set {_gift} to item_nbt_set_stat({_gift}, "TIER", {_tier})
  broadcast "%{_gift}%"
  set {_name} to name of {_gift}
  set {_tierTxt} to parser_tier({_tier})
  set name of {_gift} to "%{_name}% %{_tierTxt}%"
  give 1 of {_gift} to {_p}
  return {_gift}

function gift_consecrate(p: player, gift: item, item: item) :: item:
  set {_category} to item_nbt_get_tag({_gift}, "CATEGORY")
  set {_id} to item_nbt_get_tag({_gift}, "ID")
  set {_categoryItem} to item_nbt_get_tag({_item}, "CATEGORY")
  if {_category} is "WILL":
    set {_item} to gift_will_consecrate({_p}, {_gift}, {_item})
    set {_item} to item_nbt_set_list({_item}, "ATTRIBUTE", {SKILL::%{_categoryItem}%::%{_id}%::ATTRIBUTE::*})
  else:
    set {_item} to item_merge_stats({_gift}, {_item})
  set {_item} to item_nbt_add_list({_item}, {_category}, {_id})
  set {_item} to i18n_item({_p}, {_item})
  return {_item}

function gift_will_consecrate(p: player, gift: item, item: item) :: item:
  set {_type} to item_nbt_get_tag({_item}, "TYPE")
  if {_type} is "WEAPON":
    # We associate a skill to a weapon.
    # We first have to remove all stats of the weapon
    set {_id} to item_nbt_get_tag({_gift}, "ID")
    return weapon_will_stat({_p}, {_item}, {_id})
  return {_item}

function gift_consecrate_intellect_equipment(p: player, gift: item, item: item) :: item:
  # When consecrating an intellect gift to an equipment,
  # It has to be done on its associated skill.
  set {_id} to item_nbt_get_tag({_gift}, "ID")
  set {_item} to item_nbt_add_list({_item}, "INTELLECT", {_id})
  set {_item} to i18n_item({_p}, {_item})
  return {_item}