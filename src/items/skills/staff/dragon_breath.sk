function staff_dragon_breath_cast(p: player, staff: item):
  players_action_shootProjectile({_p}, {_staff}, snowball, 1, false)
  set {_projectile} to last shot projectile
  drawDot count 7, particle "smoke", RGB 0.2, 0.2, 0.2, center {_projectile}, visibleRange 152, pulseDelay 0, keepFor 10 ticks
  set {_loc} to location of {_p}
  set {_range} to player_stat_item({_p}, {_staff}, "RANGE")
  while {_projectile} is alive:
    if distance between {_projectile} and {_loc} > {_range}:
      staff_dragon_breath_hit({_p}, {_projectile})
      stop loop
    wait 5 ticks
  kill {_projectile}

function staff_dragon_breath_hit(p: player, projectile: entity):
  set {_location} to location of {_projectile}
  set {_power} to player_stat_projectile({_p}, {_projectile}, "POWER")
  set {_radius} to player_stat_projectile({_p}, {_projectile}, "RADIUS")
  drawDot count ceil(5*({_radius})), particle "witchspell", RGB 0.2*{_radius}, 0.2*{_radius}, 0.2*{_radius}, center {_location}, rainbowMode true, visibleRange 32, pulseDelay 0, keepFor 10 ticks
  loop all entities in radius {_radius} of {_location}:
    player_action_damage({_p}, loop-entity, 0, {_power})