function equipment_give(p: player, category: text, id: text):
  set {_equipment} to item_create({_p}, "EQUIPMENT", {_category}, {_id})
  set {_equipment} to item_nbt_set_tag_native({_equipment}, "{HideFlags:2}")
  set {_nbt} to nbt compound of {_equipment}
  set {_attrModifiers::*} to compound list tag "AttributeModifiers" of {_nbt}
  add nbt compound of "{AttributeName:""generic.armor"",Amount:0,Operation:0,UUID:[I;-12319,22491,191450,-44982],Name:""generic.armor""}" to {_attrModifiers::*}
  set compound list tag "AttributeModifiers" of {_nbt} to {_attrModifiers::*}
  give 1 of {_equipment} to {_p}

function equipment_equip(p: player, oldEquipment: item, newEquipment: item):
  loop {STAT::ALL::*}:
    set {_stat} to loop-value
    if item_nbt_isset_stat({_newEquipment}, {_stat}) is true:
      set {_oldStat} to item_nbt_get_stat({_oldEquipment}, {_stat})
      set {_newStat} to item_nbt_get_stat({_newEquipment}, {_stat})
      player_store_stat_add_lazy({_p}, {_stat}, -1*{_oldStat} + {_newStat})
    else if item_nbt_isset_stat({_oldEquipment}, {_stat}) is true:
      #old equipment had stat. New onen does not
      set {_oldStat} to item_nbt_get_stat({_oldEquipment}, {_stat})
      player_store_stat_add_lazy({_p}, {_stat}, -1*{_oldStat})
  player_store_stat_render({_p})
    

