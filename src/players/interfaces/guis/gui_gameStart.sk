function gui_gameStart_choose(gameUuid: text, p: player, type: text):
  set {_name} to i18n({_p}, "GUI.GIFT.GAME_START.%{_type}%.TITLE")
  player_store_gui_add({_p}, "GAMESTART_%{_type}%")

  create a gui with virtual chest inventory with 1 row named {_name} and shape "123456789":
    set {_it} to 0
    set {_list::*} to {%{_type}%::CATEGORY::*}
    if {_type} is "WILL":
      set {_list::*} to {GIFT::WILL::ID::*}
    else if {_type} is "INTELLECT":
      set {_list::*} to {GIFT::INTELLECT::ID::*}
    else if {_type} is "EQUIPMENT":
      remove "SHIELD" from {_list::*}

    loop {_list::*}:
      add 1 to {_it}
      set {_category} to loop-value
      set {_id} to {%{_type}%::%{_category}%::ID::1}
      if {_type} is "EQUIPMENT":
        set {_item} to equipment_create({_p}, {_category}, {_id})
      else if {_type} is "WEAPON":
        set {_item} to weapon_create({_p}, {_category}, {_id})
      else if {_type} is "WILL":
        set {_item} to gift_create({_p}, "WILL", loop-value)
      else if {_type} is "INTELLECT":
        set {_item} to gift_create({_p}, "INTELLECT", loop-value)

      make gui slot "%{_it}%" with {_item}:
        give {_item} to {_p}
        if item_nbt_get_tag({_item}, "CATEGORY") is "SWORD":
          set {_shield} to equipment_create({_p}, "SHIELD", "IRON_SHIELD")
          give {_shield} to {_p}

        player_store_gui_delete({_p}, "GAMESTART_%{_type}%")
        close inventory of {_p}
        gui_gameStart_chooseNext({_gameUuid}, {_p}, {_type})

      run on gui close:
        if player_store_gui_isset({_p}, "GAMESTART_%{_type}%") is true:
          cancel gui close
  open the last gui to {_p}

function gui_gameStart_chooseNext(gameUuid: text, p: player, currentType: text):
  set {_index} to 1
  set {_types::*} to "EQUIPMENT", "WEAPON" and "WILL"
  loop {_types::*}:
    add 1 to {_index}
    if loop-value is {_currentType}:
      stop loop
  if {_index} <= size of {_types::*}:
    gui_gameStart_choose({_gameUuid}, {_p}, {_types::%{_index}%})
  else:
    # Player has finished to choose stuff
    player_store_game_set({_p}, "STATUS", "RUNNING")
    give 1 of gift_create({_p}, "WILL", "BURNING_HEART") to {_p}

function gui_gameStart(gameUuid: text, p: player):
  player_store_gui_delete_all({_p})
  gui_gameStart_choose({_gameUuid}, {_p}, "EQUIPMENT")