function player_store_music_count(p: player) :: number:
    return size of {players::music::%{_p}%::musics::*}

function player_store_music_get_value(p: player, key: text) :: object:
    return {players::music::%{_p}%::%{_key}%}

function player_store_music_get_music_value(p: player, music: text, key: text) :: object:
    return {players::music::%{_p}%::%{_music}%::%{_key}%}

function player_store_music_set(p: player, musics: texts, playRandom: boolean = false, volume: number = 0.5):
    player_store_music_delete_all({_p})
    loop {_musics::*}:
        set {_path} to {MUSIC::%loop-value%::PATH}
        set {_duration} to {MUSIC::%loop-value%::DURATION}
        set {players::music::%{_p}%::%loop-value%::path} to {_path}
        set {players::music::%{_p}%::%loop-value%::volume} to {_volume}
        set {players::music::%{_p}%::%loop-value%::duration} to {_duration}
        set {players::music::%{_p}%::musics::*} to {_musics::*}
    set {players::music::%{_p}%::index} to 1
    set {players::music::%{_p}%::uuid} to random uuid
    player_storeMusic_play({_p})

function player_storeMusic_play(p: player):
    set {_uuid} to player_store_music_get_value({_p}, "uuid")
    set {_count} to player_store_music_count({_p})
    while {_uuid} is player_store_music_get_value({_p}, "uuid"): #While we are sill with the same loop musics
        set {_index} to player_store_music_get_value({_p}, "index")
        set {_currentMusic} to {players::music::%{_p}%::musics::%{_index}%}
        set {_volume} to {players::music::%{_p}%::%{_currentMusic}%::volume}
        set {_duration} to {players::music::%{_p}%::%{_currentMusic}%::duration}
        set {_path} to {players::music::%{_p}%::%{_currentMusic}%::path}

        play sound {_path} with volume {_volume} to {_p}
        wait "%{_duration}% ticks" parsed as timespan
        stop sound {_path} for {_p}
        set {players::music::%{_p}%::index} to max(1, mod({_index}, {_count}) + 1)

function player_store_music_delete_all(p: player):
    execute console command "stopsound %{_p}%"
    delete {players::music::%{_p}%::*}