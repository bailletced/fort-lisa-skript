# Set listeners with some optional values
function mob_store_listener_set(mob: entity, listeners: texts, keys: texts = "", values: objects = ""):
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    loop {_listeners::*}:
      set {_listener} to loop-value
      if mob_store_listener_isset({_mob}, {_listener}) is false:
        add {_listener} to {mobs::%{_uuid}%::listener::*}
        mob_store_listener_disable({_mob}, {_listener})
        if {_keys::*} is not "":
          # Some keys/vals have been passed. We store it
          loop {_keys::*}:
            set {_key} to loop-value-2
            set {_val} to {_values::%loop-index-2%}
            mob_store_listener_set_value({_mob}, {_listener}, {_key}, {_val})

# Set value for a listener
function mob_store_listener_set_value(mob: entity, listener: text, key: text, value: object):
  if {_mob} is set:
    if mob_store_listener_isset({_mob}, {_listener}) is true:
      set {_uuid} to uuid of {_mob}
      set {mobs::%{_uuid}%::listener::%{_listener}%::%{_key}%} to {_value}

# Get value stored for a listener
function mob_store_listener_get_value(mob: entity, listener: text, key: text) :: object:
  if {_mob} is set:
    if mob_store_listener_isset({_mob}, {_listener}) is true:
      set {_uuid} to uuid of {_mob}
      return {mobs::%{_uuid}%::listener::%{_listener}%::%{_key}%}

function mob_store_listener_isset(mob: entity, listener: text) :: boolean:
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    if {mobs::%{_uuid}%::listener::*} contains {_listener}:
      return true
    return false
  return false

function mob_store_listener_get_all(mob: entity) :: texts:
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    return {mobs::%{_uuid}%::listener::*}
  return ""

function mob_store_listener_get_all_enabled(mob: entity) :: texts:
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    delete {_res::*}
    loop {mobs::%{_uuid}%::listener::*}:
      if mob_store_listener_isEnabled({_mob}, loop-value) is true:
        add loop-value to {_res::*}
    return {_res::*}
  return ""

function mob_store_listener_get_all_disabled(mob: entity) :: texts:
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    delete {_res::*}
    loop {mobs::%{_uuid}%::listener::*}:
      if mob_store_listener_isEnabled({_mob}, loop-value) is false:
        add loop-value to {_res::*}
    return {_res::*}
  return ""

function mob_store_listener_count(mob: entity) :: number:
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    return size of {mobs::%{_uuid}%::listener::*}
  return 0

function mob_store_listener_enable(mob: entity, listener: text):
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    if mob_store_listener_isset({_mob}, {_listener}) is true:
      set {mobs::%{_uuid}%::listener::%{_listener}%::enabled} to true

function mob_store_listener_disable(mob: entity, listener: text):
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    if mob_store_listener_isset({_mob}, {_listener}) is true:
      set {mobs::%{_uuid}%::listener::%{_listener}%::enabled} to false

function mob_store_listener_isEnabled(mob: entity, listener: text) :: boolean:
  if {_mob} is set:
    set {_uuid} to uuid of {_mob}
    if mob_store_listener_isset({_mob}, {_listener}) is true:
      return {mobs::%{_uuid}%::listener::%{_listener}%::enabled}
    return false
  return false

function mob_store_listener_delete(mob: entity, listener: text):
  set {_uuid} to uuid of {_mob}
  remove {_listener} from {mobs::%{_uuid}%::listener::*}

function mob_store_listener_delete_all(mob: entity):
  set {_uuid} to uuid of {_mob}
  delete {mobs::%{_uuid}%::listener::*}