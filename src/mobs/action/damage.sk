function mob_action_damage(mob: entity, victim: entity):
  set {_attack} to entity_nbt_get_stat({_mob}, "ATTACK")

# We compute reduce of damage if victim is holding an item with can block damage
  if {_victim} is a player:
    set {_item} to tool of {_victim}
    if item_nbt_isset_stat({_item}, "BLOCKING") is true:
      set {_reduce} to false
      set {_blocking} to item_nbt_get_stat({_item}, "BLOCKING")
      
      set {_vMob} to vector from yaw yaw of {_mob} and pitch 0
      set {_vVictim} to vector from yaw yaw of {_victim} and pitch 0
      set {_angleYaw} to angle between {_vMob} and {_vVictim}
      if {_angleYaw} <= 20:
        # the mob's back is to the victim
        set {_reduce} to true
      else:
        set {_vMob} to vector from yaw yaw of {_mob} and pitch pitch of {_mob}
        set {_vVictim} to vector from yaw yaw of {_victim} and pitch pitch of {_victim}
        set {_angle} to angle between {_vMob} and {_vVictim}
        if abs({_angle} - 180) <= 25:
          set {_reduce} to true
      if {_reduce} is true:
        set {_attack} to {_attack} * (1 - 0.01*{_blocking})


  set {_damageCategory} to entity_nbt_get_tag({_mob}, "DAMAGE_CATEGORY")
  if {_damageCategory} is "EXPLOSION":
    set {_dist} to distance between {_mob} and {_victim}
    set {_attack} to {_attack} / max(1, {_dist})
  entity_effect_damage({_mob}, {_victim}, {_attack})