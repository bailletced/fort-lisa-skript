function mob_set_stats(mob: entity):
  set {_category} to entity_nbt_get_tag({_mob}, "CATEGORY")
  set {_id} to entity_nbt_get_tag({_mob}, "ID")
  loop {STAT::MOB::*}:
    set {_key} to loop-value
    set {_value} to {MOB::%{_category}%::%{_id}%::STAT::%{_key}%}
    if {_value} is set:
      entity_nbt_set_stat({_mob}, {_key}, {_value})
      if {_key} is "HEALTH":
        set maximum health of {_mob} to {_value}
        set health of {_mob} to {_value}

function mob_set_equipments(mob: entity):
  set {_category} to entity_nbt_get_tag({_mob}, "CATEGORY")
  set {_id} to entity_nbt_get_tag({_mob}, "ID")
  loop {EQUIPMENT::CATEGORY::*}:
    if {MOB::%{_category}%::%{_id}%::EQUIPMENT::%loop-value%} is set:
      equip {_mob} with {MOB::%{_category}%::%{_id}%::EQUIPMENT::%loop-value%} parsed as item


function mob_summon(category: text, id: text, location: location) :: entity:
  if {_category} is "ZOMBIE":
    set {_mob} to zombie_summon({_id}, {_location})

  entity_nbt_set_tag({_mob}, "TYPE", "MOB")
  entity_nbt_set_tag({_mob}, "CATEGORY", {_category})
  entity_nbt_set_tag({_mob}, "ID", {_id})
  mob_set_stats({_mob})
  mob_set_equipments({_mob})
  mob_store_add({_mob})
  return {_mob}
