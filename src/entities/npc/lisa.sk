function lisa_summon(gameUuid: text) :: entity:
  set {_world} to game_store_get({_gameUuid}, "WORLD")
  set {_location} to {LOCATION::CASTLE::THRONE}
  set world of {_location} to {_world}
  create citizen named {NPC::LISA::NAME} at {_location}
  set {_id} to last created citizen
  set {_lisa} to citizen {_id}
  entity_nbt_set_tag({_lisa}, "GAME_UUID", {_gameUuid})
  return lisa_init({_lisa})

function lisa_init(lisa: entity) :: entity:
  set {_id} to citizen id of {_lisa}
  set {_npcSkin} to {NPC::LISA::SKIN}
  execute console command "/npc select %{_id}%"
  execute console command "/npc skin --url %{_npcSkin}%" 
  execute console command "/npc lookclose"
  execute console command "/npc targetable"
  execute console command "/npc vulnerable"
  set maximum health of {_lisa} to {NPC::LISA::HEALTH}
  set health of {_lisa} to {NPC::LISA::HEALTH}
  entity_nbt_set_tag({_lisa}, "ID", "LISA")
  entity_nbt_set_stat({_lisa}, "DEFENCE", {NPC::LISA::DEFENCE})
  return {_lisa}

function lisa_listeners(lisa: entity):
  lisa_listener_INGAME({_lisa})

function lisa_listener_INGAME(lisa: entity):
  set {_gameUuid} to entity_nbt_get_tag({_lisa}, "GAME_UUID")
  set {_id} to citizen id of {_lisa}
  while game_store_get({_gameUuid}, "STATUS") is "RUNNING":
    loop all entities in radius 15 of {_lisa}:
      if type of loop-entity is not player:
        if loop-entity is not {_lisa}:
          set {_mob} to loop-entity
          mob_store_target_set({_mob}, "LISA")
          mob_store_location_delete_all({_mob})
          mob_store_listener_delete_all({_mob})
          set target of loop-entity to {_lisa}
    move citizen {_id} to {LOCATION::CASTLE::THRONE}
    wait 2 seconds
  game_end({_gameUuid})    
