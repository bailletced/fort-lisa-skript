#Return item attribute
function i18n_item_attr(p: player, item: item, attr: text) :: text:
    set {_type} to item_get_tag({_item}, "TYPE")
    set {_category} to item_get_tag({_item}, "CATEGORY")
    set {_id} to item_get_tag({_item}, "ID")
    set {_lang} to player_store_setting_get({_p}, "lang")
    return i18n({_p}, "%{_type}%.%{_category}%.%{_id}%.%{_attr}%")

#Add a i18n description to the lore of item
#params:
#   p : player
#   item : item
#   pathI18nLore : text
#Return:
#   item
function i18n_item_lore(p: player, it: item) :: text:
    set {_type} to item_get_tag({_it}, "TYPE")
    set {_category} to item_get_tag({_it}, "CATEGORY")
    set {_id} to item_get_tag({_it}, "ID")

    set {_lore} to i18n_item_attr({_p}, {_it}, "LORE")
    
    set {_stats::*} to {STAT::%{_type}%::*}
    loop {_stats::*}:
        set {_multiplier} to 0
        set {_playerStat} to 0
        set {_key} to loop-value
        set {_value} to item_get_stat({_it}, {_key})
        if item_isset_stat({_it}, "%{_key}%_MULTIPLIER") is true:
            if player_store_stat_isset({_p}, {_key}) is true:
                set {_multiplier} to item_get_stat({_it}, "%{_key}%_MULTIPLIER")
                set {_playerStat} to player_store_stat_get({_p}, {_key})
        set {_color} to {STAT::%{_key}%::COLOR}
        set {_icon} to {STAT::%{_key}%::ICON}
        set {_valueFinal} to {_value} + {_playerStat} * {_multiplier}
        replace "{_%{_key}%}" in {_lore} with "%{_color}%%{_icon}%%{_valueFinal}%§f"
    return {_lore}


#transform a lore to list to make sure it will be visible once set to an item
function i18n_item_lore_to_list(lore: text) :: texts:
    set {_lores::*} to {_lore} split at " "
    set {_text} to ""
    set {_idx} to 1
    delete {_res::*}
    loop {_lores::*}:
        set {_text} to "%{_text}% %loop-value%"
        if length of {_text} >= {LORE_LENGTH_MAX}:
            add "§f%{_text}%" to {_res::*}
            set {_text} to ""
        else if {_idx} >= size of {_lores::*}:
            set {_maxLength} to amount of {_res::*}
            if length of {_text} + length of {_res::%{_maxLength}%} > {LORE_LENGTH_MAX} + 10:
                add "§f%{_text}%" to {_res::*}
            else:
                set {_res::%{_maxLength}%} to "%{_res::%{_maxLength}%}%%{_text}%"
        add 1 to {_idx}
    return {_res::*}

# internationalize an item
function i18n_item(p: player, it: item) :: item:
    set {_title} to i18n_item_attr({_p}, {_it}, "TITLE")
    set {_lore} to i18n_item_lore({_p}, {_it})
    set {_lores::*} to i18n_item_lore_to_list({_lore})
    set lore of {_it} to {_lores::*}
    set name of {_it} to {_title}
    return {_it}