function game_start(world: world):
  set {_gameUuid} to game_store_create()
  game_store_set({_gameUuid}, "STATUS", "PREPARING")
  game_store_set({_gameUuid}, "WORLD", {_world})
  mob_store_delete_all()
  set {_lisa} to lisa_summon({_gameUuid})
  # TODO : determine which player should be part of the game
  loop all players:
    game_store_player_add({_gameUuid}, loop-player)
    player_init(loop-player, {_gameUuid})

  #GAME PREPARATION
  set {_canStart} to false
  while {_canStart} is false:
    loop game_store_players({_gameUuid}):
      set {_canStart} to true
      if player_store_game_get(loop-player, "STATUS") is "PREPARING":
        set {_canStart} to false
        stop loop
    wait 10 ticks

  #GAME FINAL INIT
  game_store_set({_gameUuid}, "STATUS", "RUNNING")
  lisa_listeners({_lisa})
  wave_init({_gameUuid})

function game_end(gameUuid: text):
  broadcast "GAME END"
  game_store_delete({_gameUuid})
  game_store_wave_set_current(0)
  execute console command "/npc remove all"
  player_interface_tablist_footer_clean_all()
  loop all entities:
    set {_mob} to loop-value
    if entity_nbt_get_tag({_mob}, "TYPE") is "MOB":
      entity_nbt_delete({_mob}, "TYPE")
      kill {_mob}
    if loop-value is a player:
      if player_store_setting_get(loop-value, "INGAME") is true:
        player_clean(loop-value)
        