function wave_init(gameUuid: text):
  wave_start(1)
  wave_listener({_gameUuid})

function wave_listener(gameUuid: text):
  game_store_wave_set_current_timer(0)
  set {_waveNumber} to game_store_wave_get_current()
  set {_waveDuration} to game_store_wave_get_current_duration()
  while game_store_get({_gameUuid}, "STATUS") is "RUNNING":
    set {_waveTimer} to game_store_wave_get_current_timer()
    wave_tablist({_waveNumber}, {_waveDuration}, {_waveTimer})
    game_store_wave_set_current_timer({_waveTimer} + 40)
    # If no mobs anymore
    if mob_count() is 0:
      wave_next({_gameUuid})
    # If timer, go to next wave
    if {_waveTimer} >= {_waveDuration}:
      wave_next({_gameUuid})
      set {_waveNumber} to game_store_wave_get_current()
      set {_waveDuration} to game_store_wave_get_current_duration()
    wait 2 seconds

function wave_tablist(waveNumber: number, waveDuration: number, waveTimer: number):
  set {_waveNumber} to game_store_wave_get_current()
  set {_waveDuration} to game_store_wave_get_current_duration()
  set {_waveTimer} to game_store_wave_get_current_timer()
  loop all players:
    set {_p} to loop-player
    set {_waveTxt} to i18n({_p}, "WAVE.TITLE")
    set tab list footer of {_p} to "%{_waveTxt}% %{_waveNumber}% - %min(100, ceil(({_waveTimer}/{_waveDuration})*100))%%%"

function wave_next(gameUuid: text):
  set {_waveNumber} to game_store_wave_get_current()
  set {_nextWave} to game_store_wave_set_next() #Go next wave
  if {_nextWave} is {_waveNumber}:
    if mob_count() is 0:
      broadcast "END OF GAME : No next wave AND NO MOBS"
      game_end({_gameUuid})
  else:
    wave_start({_nextWave})

function wave_start(waveNumber: number):
  game_store_wave_set_current({_waveNumber})
  game_store_wave_set_current_timer(0)
  load yaml "plugins/Skript/scripts/fort-lisa/config/waves/%{_waveNumber}%.yml" as "YAML-WAVE"
  delete {_loadedIds::*}
  loop node keys "MOBS" of "YAML-WAVE":
    set {_group} to loop-key
    set {_id} to yaml value "MOBS.%{_group}%.ID" from "YAML-WAVE"
    set {_level} to yaml value "MOBS.%{_group}%.LEVEL" from "YAML-WAVE"
    set {_amount} to yaml value "MOBS.%{_group}%.AMOUNT" from "YAML-WAVE"
    set {_delay} to yaml value "MOBS.%{_group}%.DELAY" from "YAML-WAVE"
    set {_ai} to yaml value "MOBS.%{_group}%.AI" from "YAML-WAVE"
    if yaml node "MOBS.%{_group}%.LOCATION" from "YAML-WAVE" has list:
      set {_isRandomLoc} to true
      set {_possibleLocs::*} to yaml list "MOBS.%{_group}%.LOCATION" from "YAML-WAVE"
    else:
      set {_location} to yaml value "MOBS.%{_group}%.LOCATION" from "YAML-WAVE"
      set {_location::*} to {_location} split at "."
      set {_location} to {LOCATION::%{_location::1}%::%{_location::2}%}

    if {_loadedIds::*} does not contain {_id}:
      add {_id} to {_loadedIds::*}
      # We load yaml mob file
      set {_idPath} to {_id} in lower case
      replace "_" with "/" in {_idPath}
      load yaml "plugins/Skript/scripts/fort-lisa/config/mobs/%{_idPath}%.yml" as "YAML-MOB-%{_id}%"

    loop {_amount} times:
      if {_isRandomLoc} is true:
        set {_location} to random element of {_possibleLocs::*}
        set {_location::*} to {_location} split at "."
        set {_location} to {LOCATION::%{_location::1}%::%{_location::2}%}
      broadcast "%{_location}%"
      mob_summon({_id}, {_level}, {_ai}, {_location})
      wait "%{_delay}% ticks" parsed as timespan
  
  # We unload yaml mob files
  loop {_loadedIds::*}:
    unload yaml "YAML-MOB-%loop-value%"

  unload YAML "YAML-WAVE"