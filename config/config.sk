function config_load(world: world):
  #STATS
  load yaml "plugins/Skript/scripts/fort-lisa/config/stats.yml" as "YAML-STATS"
  delete {STAT::*}
  set {STAT::WEAPON::*} to yaml list "WEAPON" from "YAML-STATS"
  set {STAT::SKILL::*} to yaml list "SKILL" from "YAML-STATS"
  set {STAT::PLAYER::*} to yaml list "PLAYER" from "YAML-STATS"
  set {STAT::MOB::*} to yaml list "MOB" from "YAML-STATS"
  set {STAT::NPC::*} to yaml list "NPC" from "YAML-STATS"
  loop yaml node keys "ALL" of "YAML-STATS":
    add loop-value to {STAT::ALL::*}
    set {_icon} to yaml value "ALL.%loop-value%.ICON" from "YAML-STATS"
    set {STAT::%loop-value%::ICON} to yaml value "ALL.%loop-value%.ICON" from "YAML-STATS"
    set {STAT::%loop-value%::COLOR} to yaml value "ALL.%loop-value%.COLOR" from "YAML-STATS"
  unload yaml "YAML-STATS"

  #SKILLS
  load yaml "plugins/Skript/scripts/fort-lisa/config/items/skills.yml" as "YAML-SKILLS"
  delete {SKILL::*}
  loop yaml node keys "" of "YAML-SKILLS":
    set {_category} to loop-value
    add {_category} to {SKILL::CATEGORY::*}
    loop yaml node keys "%{_category}%" of "YAML-SKILLS":
      set {_id} to loop-key-2
      add {_id} to {SKILL::%{_category}%::*}
      set {SKILL::%{_category}%::%{_id}%::ITEM} to yaml value "%{_category}%.%{_id}%.ITEM" from "YAML-SKILLS" parsed as item
      loop yaml node keys "%{_category}%.%{_id}%.STATS" of "YAML-SKILLS":
        set {SKILL::%{_category}%::%{_id}%::%loop-key-3%} to yaml value "%{_category}%.%{_id}%.STATS.%loop-key-3%" from "YAML-SKILLS"
  unload yaml "YAML-SKILLS"

  #WEAPONS
  load yaml "plugins/Skript/scripts/fort-lisa/config/items/weapons.yml" as "YAML-WEAPONS"
  delete {WEAPON::*}
  loop yaml node keys "" of "YAML-WEAPONS":
    set {_category} to loop-value
    set {WEAPON::CATEGORY::%{_category}%} to {_category}
    add {_category} to {WEAPON::CATEGORY::*}
    loop yaml node keys "%{_category}%" of "YAML-WEAPONS":
      set {_id} to loop-key-2
      add {_id} to {WEAPON::%{_category}%::*}
      set {WEAPON::%{_category}%::%{_id}%::ITEM} to yaml value "%{_category}%.%{_id}%.ITEM" from "YAML-WEAPONS" parsed as item
      loop yaml node keys "%{_category}%.%{_id}%.STATS" of "YAML-WEAPONS":
        set {WEAPON::%{_category}%::%{_id}%::%loop-key-3%} to yaml value "%{_category}%.%{_id}%.STATS.%loop-key-3%" from "YAML-WEAPONS"
  unload yaml "YAML-WEAPONS"

  #MOBS
  load yaml "plugins/Skript/scripts/fort-lisa/config/mobs/mobs.yml" as "YAML-MOB"
  load yaml "plugins/Skript/scripts/fort-lisa/config/mobs/zombies.yml" as "YAML-MOB-ZOMBIE"
  delete {MOB::*}
  loop yaml list "CATEGORIES" from "YAML-MOB":
    set {_category} to loop-value
    loop node keys "" of "YAML-MOB-%{_category}%":
      set {_id} to loop-value-2
      loop yaml node keys {_id} of "YAML-MOB-%{_category}%":
        set {_stat} to loop-key-3
        set {_value} to yaml value "%{_id}%.%{_stat}%" from "YAML-MOB-%{_category}%"
        set {MOB::%{_category}%::%{_id}%::%{_stat}%} to {_value}
  unload yaml "YAML-MOB"
  unload yaml "YAML-MOB-ZOMBIE"

  #LOCATIONS
  load yaml "plugins/Skript/scripts/fort-lisa/config/locations/map-castle.yml" as "YAML-LOCATION-MAP-CASTLE"
  delete {LOCATION::*}
  set {LOCATION::CASTLE::THRONE} to location_text2Loc(yaml value "CASTLE.THRONE" from "YAML-LOCATION-MAP-CASTLE", {_world})
  
  set {_spawnsFar::*} to yaml list "SPAWNS.FAR" from "YAML-LOCATION-MAP-CASTLE"
  loop {_spawnsFar::*}:
    set {_location} to location_text2Loc(loop-value, {_world})
    add {_location} to {LOCATION::SPAWN::FAR::*}

  set {_spawnsMedium::*} to yaml list "SPAWNS.MEDIUM" from "YAML-LOCATION-MAP-CASTLE"
  loop {_spawnsMedium::*}:
    set {_location} to location_text2Loc(loop-value, {_world})
    add {_location} to {LOCATION::SPAWN::MEDIUM::*}

  set {_castleEntrances::*} to yaml list "CASTLE.ENTRANCES" from "YAML-LOCATION-MAP-CASTLE"
  loop {_castleEntrances::*}:
    set {_location} to location_text2Loc(loop-value, {_world})
    add {_location} to {LOCATION::CASTLE::ENTRANCE::*}
  unload yaml "YAML-LOCATION-MAP-CASTLE"

  #NPCS
  load yaml "plugins/Skript/scripts/fort-lisa/config/npcs.yml" as "YAML-NPC"
  loop node keys "" from "YAML-NPC":
    set {_id} to loop-value
    loop yaml node keys "%{_id}%" from "YAML-NPC":
      set {_key} to loop-value-2
      set {NPC::%{_id}%::%{_key}%} to yaml value "%{_id}%.%{_key}%" from "YAML-NPC"
  unload yaml "YAML-NPC"

  #UTILITIES
  load yaml "plugins/Skript/scripts/fort-lisa/config/items/utilities.yml" as "YAML-UTILITIES"
  loop yaml node keys "" from "YAML-UTILITIES":
    set {_id} to loop-value
    set {UTILITY::%{_id}%::ITEM} to yaml value "%{_id}%.ITEM" from "YAML-UTILITIES" parsed as item
  unload yaml "YAML-UTILITIES"

  #LANGS
  load yaml "plugins/Skript/scripts/fort-lisa/config/langs/fr-FR.yml" as "YAML-LANG-fr-FR"
  load yaml "plugins/Skript/scripts/fort-lisa/config/langs/en-EN.yml" as "YAML-LANG-en-EN"

  #GLOBAL VAR
  set {LORE_LENGTH_MAX} to 40
  set {LANG_FALLBACK} to "fr-FR"
  set {HOTBAR_SLOTS::*} to 0,1,2,3,4,5,6,7 and 8
  set {GAME::STATUS} to "WAITING_FOR_START"
  set {NaN} to -999

  # Initialize players. TODO : to be triggered when game starts
  loop all players:
    set {_p} to loop-value
    player_store_weapon_delete_all({_p})
    player_store_setting_set({_p}, "INGAME", false)
    wait 20 ticks
    player_store_setting_set({_p}, "INGAME", true)
    onPeriodTenTicks({_p})